apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    api files('libs/alipaySdk-15.5.9-20181123210601.jar')
}

version = "1.0.0"
group = "com.pingerx"
def siteUrl = 'https://github.com/pingerx/alipay-sdk'    // 项目主页
def gitUrl = 'https://github.com/pingerx/alipay-sdk.git' // 项目的git地址

install {
    repositories.mavenInstaller {
        pom.project {
            groupId 'com.pingerx'
            artifactId 'alipay-sdk'
            packaging 'aar'
            name 'alipay-sdk'
            description 'alipay sdk with aar file.'
            url siteUrl
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'     // 开源协议名称
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt' // 协议地址
                }
            }
            developers {
                developer {
                    id 'pingerx'               // 账号
                    name 'pingerx'             // 名称
                    email 'xmpinger@gmail.com' // 邮箱地址
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    // 读取配置文件中的用户名和key
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        userOrg = 'fungo'
        repo = "maven"                   // 你在bintray上创建的库的名称
        name = "alipay-sdk"              // 在jcenter中的项目名称
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}

// gradlew build
// gradlew install
// gradlew bintrayUpload
